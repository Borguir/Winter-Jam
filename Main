extends Node2D

enum EstadoClima {NORMAL, VIENTO}
var estado_clima = EstadoClima.NORMAL
var evento_activo = false 
var tiempo_proximo_evento = 10.0 

@export var dificultad_global: float = 3.2
@export var incremento_dificultad: float = 0.4
@onready var spawners = $Spawners.get_children()
@onready var camino = $Camino
@onready var barra_progreso = $CanvasLayer/ProgressBar

var player_s = preload("res://Escenas/player.tscn")
var aviso_scene = preload("res://Escenas/Aviso.tscn")
var pez_scene = preload("res://Escenas/pez.tscn")

var player = null
var puntos = 0
var vidas = 4
var dificultad_max = 20.0
var dificultad_min = 1.0
var contador_para_peces = 0
var meta_para_peces = 2
var distancia_meta = 3000.0
var distancia_recorrida = 0.0
var juego_terminado = false

func _ready():
	$SpawnTimer.start()
	spawn_player()
	if player:
		player.position = Vector2(90.0, 250.0)
		await get_tree().process_frame
		if player: player.y_suelo_real = player.position.y

func _process(delta):
	if vidas > 0 and not evento_activo:
		tiempo_proximo_evento -= delta
		if tiempo_proximo_evento <= 0:
			tiempo_proximo_evento = randf_range(15.0, 25.0)
			decidir_evento()
		var avance = dificultad_global * 5.0 * delta
		distancia_recorrida += avance
		var porcentaje = (distancia_recorrida / distancia_meta) * 100
		barra_progreso.value = porcentaje
		if distancia_recorrida >= distancia_meta:
			llegar_iglu()

func decidir_evento():
	if dificultad_global < 4.0: return
	if dificultad_global > 5.0:
		activar_viento()

func _on_spawn_timer_timeout():
	spawn_normal()

func spawn_normal():
	var intentos_spawn = [] 
	for marker in spawners:
		if marker.has_method("generar_spawn_potencial"):
			var objeto = marker.generar_spawn_potencial(dificultad_global)
			intentos_spawn.append({ "nodo": objeto, "marker": marker })
	var indices_peligrosos = []
	for i in range(intentos_spawn.size()):
		var obj = intentos_spawn[i]["nodo"]
		if obj != null and "tipo_objeto" in obj:
			if obj.tipo_objeto == 0 or obj.tipo_objeto == 3:
				indices_peligrosos.append(i)

	if indices_peligrosos.size() >= 3:
		var indice_borrar = indices_peligrosos.pick_random()
		if intentos_spawn[indice_borrar]["nodo"]:
			intentos_spawn[indice_borrar]["nodo"].queue_free()
			intentos_spawn[indice_borrar]["nodo"] = null

	contador_para_peces += 1
	var pez_extra_data = null 

	if contador_para_peces >= meta_para_peces:
		contador_para_peces = 0
		meta_para_peces = randi_range(2, 4) 
		var idx_azar = randi() % intentos_spawn.size()
		var contenido_carril = intentos_spawn[idx_azar]["nodo"]
		var marker_elegido = intentos_spawn[idx_azar]["marker"]
		var poner_pez = false
		var debe_ser_alto = false
		
		if contenido_carril == null:
			poner_pez = true
			debe_ser_alto = randi() % 2 == 0 
		elif "tipo_objeto" in contenido_carril:
			if contenido_carril.tipo_objeto == 0:
				poner_pez = true
				debe_ser_alto = true
			elif contenido_carril.tipo_objeto == 3:
				poner_pez = false
		if poner_pez:
			pez_extra_data = {
				"marker": marker_elegido, 
				"es_alto": debe_ser_alto
			}
	for item in intentos_spawn:
		var obj = item["nodo"]
		var marker = item["marker"]
		if obj != null:
			obj.global_position = marker.global_position
			add_child(obj)
	if pez_extra_data != null:
		spawnear_pez_extra(pez_extra_data["marker"], dificultad_global, pez_extra_data["es_alto"])

func spawn_diagonal():
	var direccion = randi() % 2 
	var offset_y = -10.0
	if spawners.is_empty() or spawners[0].escenas.is_empty(): 
		return
	
	var indice_azar = randi() % spawners[0].escenas.size()
	var escena_base = spawners[0].escenas[indice_azar]
	var temp = escena_base.instantiate()
	var es_arbol = false
	if "tipo_objeto" in temp and temp.tipo_objeto == 3:
		es_arbol = true
	temp.queue_free()
	var carril_hueco = -1
	if es_arbol:
		carril_hueco = randi() % spawners.size()
	
	for i in range(spawners.size()):
		if es_arbol and i == carril_hueco:
			continue 
		var marker = spawners[i]
		var index_ajustado = i
		if direccion == 1:
			index_ajustado = (spawners.size() - 1) - i
		var obs = escena_base.instantiate()
		obs.global_position = marker.global_position
		obs.position.y += (index_ajustado * offset_y)
		if "dificultad" in obs: obs.dificultad = dificultad_global
		add_child(obs)
		
		if obs.tipo_objeto == 1:
			if randi() % 100 < 30:
				spawnear_pez_extra(marker, dificultad_global, true)

func spawnear_pez_extra(marker, dificultad, es_alto):
	var pez = pez_scene.instantiate()
	pez.global_position = marker.global_position
	pez.dificultad = dificultad
	pez.tipo_objeto = 2
	pez.es_alto = es_alto
	add_child(pez)

func _on_dificult_timer_timeout() -> void:
	dificultad_global += incremento_dificultad
	dificultad_global = min(dificultad_global, dificultad_max)
	print("Dificultad: ", dificultad_global)
	
	if camino:
		camino.autoscroll.y = 30 * dificultad_global 
	if $SpawnTimer.wait_time > 0.6:
		$SpawnTimer.wait_time -= 0.05

func spawn_player():
	var instanciar = player_s.instantiate()
	add_child(instanciar)
	player = instanciar
	player.add_to_group("Player")

func sumar_puntos(amount: int):
	puntos += amount
	print("puntos: " + str(puntos))

func restar_puntos(amount: int):
	puntos -= amount
	if puntos < 0: puntos = 0

func choques():
	vidas -= 1
	if vidas <= 0:
		game_over()
	else:
		bajar_dificultad()

func bajar_dificultad():
	dificultad_global = dificultad_global / 2.0
	dificultad_global = max(dificultad_min, dificultad_global)
	
	$SpawnTimer.wait_time = min(1.5, $SpawnTimer.wait_time + 0.3)
	
	if camino:
		camino.autoscroll.y = 30 * dificultad_global
	
	if player:
		var tween = create_tween()
		tween.tween_property(player, "modulate:a", 0.5, 0.1)
		tween.tween_property(player, "modulate:a", 1.0, 0.1)
		tween.set_loops(3)

func activar_viento():
	if evento_activo: return
	evento_activo = true
	var dir = [-1, 1].pick_random()
	print("Viento: ", dir)
	
	if player and player.has_node("ParticulasViento"):
		player.get_node("ParticulasViento").direction.x = dir
		player.get_node("ParticulasViento").emitting = true
	
	var tween = create_tween()
	tween.tween_property(player, "fuerza_viento", dir * 50.0, 2.0)
	await get_tree().create_timer(8.0).timeout
	
	if player:
		var tween_off = create_tween()
		tween_off.tween_property(player, "fuerza_viento", 0.0, 2.0)
		tween_off.finished.connect(func(): 
			evento_activo = false
			if player and player.has_node("ParticulasViento"):
				player.get_node("ParticulasViento").emitting = false
		)
	else:
		evento_activo = false

func game_over():
	if player:
		if player.has_method("descansar"): player.descansar()
		else: player.perder()

func llegar_iglu():
	juego_terminado = true
	print("Â¡Hogar dulce hogar!")
	$SpawnTimer.stop()
	get_tree().call_group("obstaculos", "queue_free")
	#Poner lo demas
